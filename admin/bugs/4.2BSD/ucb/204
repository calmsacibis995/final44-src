Return-Path: <mit-erl!gildea@mit-eddie.ARPA>
Date: Fri, 7 Jun 85 20:32:32 edt
From: Stephen Gildea <mit-erl!gildea@mit-eddie.ARPA>
Site: MIT Earth Resources Lab, Cambridge Mass.
Subject: improved version of sendbug
Index: ucb/sendbug 4.2BSD Fix

Description:
	Sendbug is not as smart as it could be.  For example, it
	should use the environment variable EDITOR.  It should
	give the user some easy way of aborting the process (e.g.,
	by not writing out the file.  The skeleton file should be
	more helpful.
Repeat-By:
	Just try aborting a bug report, or using an alternate editor,
	or giving the program to a nieve user (ok, so perhaps nieve
	users shouldn't be using it in the first place).
Fix:
	Here is a better version of sendbug/sendbug.sh and sendbug/bugformat.
------------------------------sendbug.sh------------------------------
#! /bin/csh -f
#
#	sendbug.sh	4.2	83/05/23
# Create a bug report and mail to '4bsd-bugs'.
#
# Enhanced considerably by gildea Jun 85
#

set mailer=/usr/lib/sendmail
set exitstatus=1
onintr clean
/bin/cp /usr/ucb/bugformat /tmp/bug$$
set oldline=`/bin/ls -l /tmp/bug$$`
if (! ${?EDITOR}) then
	setenv EDITOR /usr/ucb/vi
endif
$EDITOR /tmp/bug$$
set newline=`/bin/ls -l /tmp/bug$$`
if ("$newline" != "$oldline") then
	if ($#argv == 0) then
		set arg=-t
	else
		set arg=$argv[1]
	endif
	sed -e 's/^--Text follows this line--$//' /tmp/bug$$ | $mailer $arg
	echo sendbug: bug report mailed
	set exitstatus=0
else
	echo sendbug: bug report aborted
endif
clean:
/bin/rm -f /tmp/bug$$
exit($exitstatus)
---------------------------end sendbug.sh-------------------------------
------------------------------bugformat---------------------------------
	To: 4bsd-bugs@berkeley
	Subject: Short summary of the problem
	Index:	<source directory>/<source file> 4.2BSD

Description:
	Detailed description of the problem, suggestion, or complaint.
Repeat-By:
	Procedure to repeat the problem.
Fix:
	Description of how to fix the problem.
	If you don't know the fix, don't include this section or "Fix:".
	If you do include this section, add " Fix" to the end of the 
	Subject line after the 4.2BSD

---------- Remove this line and what's below it. (only for reference) ------
<source directory> ::=  bin | etc | games | ideas | lib | local | man |
			misc | sys | ucb | usr.bin | usr.lib

When finished editing the bug report, save the file and exit the editor;
the report will get mailed automatically.
To abort, exit without saving the file.
------------------------------end bugformat------------------------------
