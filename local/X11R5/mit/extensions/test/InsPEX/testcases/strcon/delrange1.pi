
/* $XConsortium: delrange1.pi,v 5.1 91/02/16 10:00:48 rws Exp $ */

/*****************************************************************
Copyright (c) 1989, 1990, 1991,1990 by Sun Microsystems, Inc. and the X Consortium.

                        All Rights Reserved

Permission to use, copy, modify, and distribute this software and its 
documentation for any purpose and without fee is hereby granted, 
provided that the above copyright notice appear in all copies and that
both that copyright notice and this permission notice appear in 
supporting documentation, and that the names of Sun Microsystems,
the X Consortium, and MIT not be used in advertising or publicity 
pertaining to distribution of the software without specific, written 
prior permission.  

SUN MICROSYSTEMS DISCLAIMS ALL WARRANTIES WITH REGARD TO THIS SOFTWARE, 
INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS, IN NO EVENT 
SHALL SUN MICROSYSTEMS BE LIABLE FOR ANY SPECIAL, INDIRECT OR CONSEQUENTIAL 
DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS,
WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION,
ARISING OUT OF OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS
SOFTWARE.

******************************************************************/

## delrange1.pi - test DELETE ELEMENT RANGE with empty structure,
## deleting the only element in a structure,
## and in the middle of a loaded structure with ptr1 {<,>,==} ptr2

tbegintest("delrange1", "DELETE ELEMENT RANGE w/ ptr1 <,>,= ptr2");

source macro.pi		/* general macros */
source strmacro.pi	/* macros specific to structure content area */
source apimacro.pi	/* macros isolating api-specific functions */

/*
 * im_confirm_struct macro confirms that the specific structure used
 * in this test is intact
 */
macro im_confirm_struct()
    foreach @ind @elem (1; PELEM_LINETYPE;
			2; PELEM_LABEL;
			3; PELEM_MARKER_TYPE;
			4; PELEM_TEXT_IND;
			5; PELEM_EDGE_IND; )
	im_confirm_elemtype(1, @ind, @elem,
		"failed: element @ind was %d, expected @elem\n");
    end
endmacro

# Set up phigs, workstation, and structure
i_open_phigs(NULL,PDEF_MEM_SIZE);
i_open_ws(1);
popen_struct(1);
ppost_struct(1,1,1.0);

# DELETE ELEMENT RANGE with empty structure
pdel_elem_range(0,99);

# add one element, delete it with DELETE ELEMENT RANGE
plabel(1);
pdel_elem_range(0,99);
im_confirm_elemptr(0, "failed: elem ptr %d expected 0 after deleting only elt.\n");

# Add some elements to the structure, confirm results
# (struct should become: (LINETYPE, LABEL, MARKERTYPE,TEXT_INDEX,EDGE_INDEX))
pset_linetype(1);
plabel(2);
pset_marker_type(3);
pset_text_ind(4);
pset_edge_ind(5);
im_confirm_struct();

# DELETE ELEMENT RANGE with ptr1 < ptr2
# (struct should become: (LINETYPE, EDGE_INDEX))
pdel_elem_range(2,4);
im_confirm_elemptr(1, "failed: elem ptr %d should be 1 after deleting 2-4\n");
im_confirm_elemtype(1, 2, PELEM_EDGE_IND,
	"failed: element 2 should be EDGE_INDEX, not %d\n");

# re-create original structure
pset_elem_ptr(1);
plabel(2);
pset_marker_type(3);
pset_text_ind(4);
im_confirm_struct();

# DELETE ELEMENT RANGE with ptr1 > ptr2
# (struct should become: (LINETYPE, LABEL,EDGE_INDEX))
pdel_elem_range(4,3);
im_confirm_elemptr(2, "failed: elem ptr %d should be 2 after pdel_elem_range(4-3)\n");
im_confirm_elemtype(1, 2, PELEM_LABEL,
	"failed: element 2 should be LABEL, not %d, after pdel_elem_range\n");
im_confirm_elemtype(1, 3, PELEM_EDGE_IND,
	"failed: element 3 should be EDGE_INDEX, not %d after pdel_elem_range\n");

# DELETE ELEMENT RANGE with ptr1 == ptr2
# struct should become: (LINETYPE, EDGE_INDEX)
pdel_elem_range(2,2);
im_confirm_elemptr(1, "failed: elem ptr %d should be 1 after pdel_elem_range(2,2)\n");
im_confirm_elemtype(1, 1, PELEM_LINETYPE,
	"failed: repeated pdel_elem_range changed elem 1 from LINETYPE to %d\n");
im_confirm_elemtype(1, 2, PELEM_EDGE_IND,
	"failed: element 2 should be EDGE_INDEX, not %d\n");

pclose_struct();
pclose_ws(1);
pclose_phigs();
tendtest();
