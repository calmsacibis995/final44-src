/* $XConsortium: evalmap.pi,v 5.1 91/02/16 10:01:03 rws Exp $ */

/*****************************************************************
Copyright (c) 1989, 1990, 1991,1990 by Sun Microsystems, Inc. and the X Consortium.

                        All Rights Reserved

Permission to use, copy, modify, and distribute this software and its 
documentation for any purpose and without fee is hereby granted, 
provided that the above copyright notice appear in all copies and that
both that copyright notice and this permission notice appear in 
supporting documentation, and that the names of Sun Microsystems,
the X Consortium, and MIT not be used in advertising or publicity 
pertaining to distribution of the software without specific, written 
prior permission.  

SUN MICROSYSTEMS DISCLAIMS ALL WARRANTIES WITH REGARD TO THIS SOFTWARE, 
INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS, IN NO EVENT 
SHALL SUN MICROSYSTEMS BE LIABLE FOR ANY SPECIAL, INDIRECT OR CONSEQUENTIAL 
DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS,
WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION,
ARISING OUT OF OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS
SOFTWARE.

******************************************************************/


## evalmap.pi -  
## Test matrix utility functions peval_view_map_matrix, peval_view_map_matrix3
## Test using different 
##   window,viewport: default,square,non-square,different aspect ratio 
##   projection types: PARALLEL,PERSPECTIVE
##   projection reference point: orthogonal,oblique
##   front,view,back plane distances: F==V,V==B,F>V>B
## The resulting matrix from the utility is compared to the matrix 
## from the same operation performed in the test code.

source macro.pi
tbegintest("evalmap","eval mapping matrix utilities")

# SETUP: open phigs
i_open_phigs(NULL,PDEF_MEM_SIZE);

foreach @wxmin @wxmax @wymin @wymax 
	@vxmin @vxmax @vymin @vymax @vzmin @vzmax (
	0.0;1.0;0.0;1.0;
	0.0;1.0;0.0;1.0;0.0;1.0;

	-2.0;2.0;-2.0;2.0;
	0.0;1.0;0.0;1.0;0.0;1.0;

	0.0;1.0;0.0;1.0;
	0.0;0.5;0.0;0.5;0.0;0.5;

	0.0;0.5;0.0;1.0;
	0.0;1.0;0.0;1.0;0.0;1.0;

	0.0;1.0;0.0;1.0;
	0.0;0.5;0.0;1.0;0.0;0.5;
	)

	# INITIALIZE: window, viewport, and viewmapping 2D
	window = I_Plimit(@wxmin,@wxmax,@wymin,@wymax);
	viewport = I_Plimit(@vxmin,@vxmax,@vymin,@vymax);
	viewmap = I_Pview_map(window,viewport);

	# OPERATION:  peval_view_map_matrix
	err = i_bogus_error();
	peval_view_map_matrix(&viewmap,&err,mtx1);
	im_ifneq(err,0,"ERROR: %d in peval_view_map_matrix",err);

	# VERIFICAION: verify the resulting mtx 2D
	i_evalviewmappingmtx(viewmap,mtx2);
	status = i_compare_Pmatrix(mtx1,mtx2);
	if (status != 1)
		tvprintf(1,"window(%f,%f,%f,%f),viewport(%f,%f,%f,%f)\n",
		    @wxmin,@wxmax,@wymin,@wymax,@vxmin,@vxmax,@vymin,@vymax);
		tfprintf("failed: peval_view_map_matrix mtx != expected\n",
			NULL);
	endif

	# testing pevalviewmappintmatrix3

	foreach @proj (
		PTYPE_PERSPECT; 
		PTYPE_PARAL;
		)
		foreach @prpx @prpy @prpz (
			0.0;0.0;10.0;
			1.0;1.0;10.0;
			)
			foreach @fpd @vpd @bpd (
				1.0; 1.0; 0.0;
				1.0; 0.0; 0.0;
				0.9; 0.5; 0.1;
			)
	# INITIALIZE: viewport, projection reference pt, viewmapping 3D
	viewport3 = I_Plimit3(@vxmin,@vxmax,@vymin,@vymax,@vzmin,@vzmax);
	prp = I_Ppoint3(@prpx,@prpy,@prpz);
	viewmap3 = I_Pview_map3(window,viewport3,@proj,prp,@vpd,@bpd,@fpd);
	# OPERATION:  peval_view_map_matrix3
	err = i_bogus_error();
	peval_view_map_matrix3(&viewmap3,&err,mtx31);
	im_ifneq(err,0,"ERROR: %d in peval_view_map_matrix3",err);

	# VERIFICAION: verify the resulting mtx 3D
	i_evalviewmappingmtx3(viewmap3,mtx32);
	status = i_compare_Pmatrix3(mtx31,mtx32);
	if (status != 1)
		tvprintf(1,"window(%f,%f,%f,%f),viewport(%f,%f,%f,%f,%f,%f)\n",
		    	@wxmin,@wxmax,@wymin,@wymax,
		    	@vxmin,@vxmax,@vymin,@vymax,@vzmin,@vzmax);
		tvprintf(1,"prp(%f,%f,%f),vpd(%f),bpd(%f),fpd(%f)proj(@proj)\n",
		    	@prpx,@prpy,@prpz,@vpd,@bpd,@fpd);
		tfprintf("failed: peval_view_map_matrix mtx != expected\n"
			,NULL);
	endif

			end
		end
	end
end
tendtest()
