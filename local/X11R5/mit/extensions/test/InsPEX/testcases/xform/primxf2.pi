
/* $XConsortium: primxf2.pi,v 5.2 91/07/24 18:47:58 hersh Exp $ */

/*****************************************************************
Copyright (c) 1989, 1990, 1991,1990 by Sun Microsystems, Inc. and the X Consortium.

                        All Rights Reserved

Permission to use, copy, modify, and distribute this software and its 
documentation for any purpose and without fee is hereby granted, 
provided that the above copyright notice appear in all copies and that
both that copyright notice and this permission notice appear in 
supporting documentation, and that the names of Sun Microsystems,
the X Consortium, and MIT not be used in advertising or publicity 
pertaining to distribution of the software without specific, written 
prior permission.  

SUN MICROSYSTEMS DISCLAIMS ALL WARRANTIES WITH REGARD TO THIS SOFTWARE, 
INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS, IN NO EVENT 
SHALL SUN MICROSYSTEMS BE LIABLE FOR ANY SPECIAL, INDIRECT OR CONSEQUENTIAL 
DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS,
WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION,
ARISING OUT OF OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS
SOFTWARE.

******************************************************************/

## primxf2.pi -
## Test view clipping with different primitives


source macro.pi

/* 
 * Test clipping
 * Verify the existence of pixel with colour index 1 at test point 1
 * and absence of pixel with colour index 1 at test point 2
 */
macro im_vclip_test1(@msg)
	im_confirm_point3(xdisplay,xwindow,1,1,testpt1,1,5,1,@msg);
	im_confirm_point3(xdisplay,xwindow,1,1,testpt2,0,5,1,@msg);
	i_pause();
	pdel_elem();
endmacro

/* 
 * Test clipping and also test that boundary created by clipping is not 
 * rendered as an edge of the polygon.
 * verify the existence of pixel with colour index 1 at test point 3
 * and absence of pixel with colour index 1 at test point 4 & 5
 */
macro im_vclip_test2(@msg)
	im_confirm_point3(xdisplay,xwindow,1,1,testpt3,1,5,1,@msg);
	im_confirm_point3(xdisplay,xwindow,1,1,testpt3,1,5,2,@msg);
	im_confirm_point3(xdisplay,xwindow,1,1,testpt4,0,5,1,@msg);
	im_confirm_point3(xdisplay,xwindow,1,1,testpt4,1,5,2,@msg);
	im_confirm_point3(xdisplay,xwindow,1,1,testpt5,0,5,1,@msg);
	im_confirm_point3(xdisplay,xwindow,1,1,testpt5,0,5,2,@msg);
	i_pause();
	pdel_elem();
endmacro

/* 
 * Test clipping and also test that boundary created by clipping is rendered 
 * as a boundary for a hollow polygon
 * Verify the existence of pixel with colour index 1 at test point 3 & 4
 * and absence of pixel with colour index 1 at test point 5
 */
macro im_vclip_test3(@msg)
	im_confirm_point3(xdisplay,xwindow,1,1,testpt3,1,5,2,@msg);
	im_confirm_point3(xdisplay,xwindow,1,1,testpt4,1,5,2,@msg);
	im_confirm_point3(xdisplay,xwindow,1,1,testpt5,0,5,2,@msg);
	i_pause();
	pdel_elem();
endmacro

tbegintest("primxf2",
	"view clipping with different primitives");

# setup: open phigs, open workstation, open structure

i_open_phigs(NULL,0)
i_open_x_ws(1,&xdisplay,&xwindow,0,0,200,200);
popen_struct(1)
ppost_struct(1,1,1.0)
pset_anno_char_ht(0.03);
pset_char_ht(0.03);
pset_int_style(PSTYLE_SOLID);
pset_int_colr_ind(2);
pset_edge_flag(PEDGE_ON);

# initialize window, viewport, cliplimit and projection reference point
#  for creating view mapping matrix, and test points
window = I_Plimit(0.0,0.5,0.0,1.0);
viewport3 = I_Plimit3(0.0,0.5, 0.0,1.0, 0.0,1.0);
cliplimit3 = I_Plimit3(0.0,0.5, 0.0,1.0, 0.0,1.0);
prp3 = I_Ppoint3(0.0,0.0,20.0);

testpt1 = I_Ppoint3(0.4,0.5,0.0);
testpt2 = I_Ppoint3(0.6,0.5,0.0);

testpt3 = I_Ppoint3(0.4,0.4,0.0);
testpt4 = I_Ppoint3(0.5,0.45,0.0);
testpt5 = I_Ppoint3(0.6,0.4,0.0);

# initialize view orientation and view mapping mtx to identity,
i_identity3(orientmtx3);
i_identity3(mapmtx3);

# initialize view representation
viewrep3 = I_Pview_rep3(orientmtx3,mapmtx3,cliplimit3,PIND_CLIP,PIND_NO_CLIP,PIND_NO_CLIP);
 
# set view represention for view index 1, use view index 1 for rendering
pset_view_rep3(1,1,&viewrep3);
pset_view_ind(1);

# draw different primitives 

# 1. annotation text
atxpos = I_Ppoint3(0.4,0.5,0.0);
atxoffset = I_Ppoint3( 0.0,0.0,0.0);
panno_text_rel3(&atxpos,&atxoffset,"annotation text");
im_vclip_test1("annotation text");

# 2. nurb curve
cfloatarr = IA_Pfloat(7, 0.0,0.0,0.0,0.1,0.2,0.2,0.2);
ncknots = I_Pfloat_list(7, cfloatarr);
ncpts = IA_Ppoint3(4, 0.4,0.5,0.0, 0.45,0.6,1.0, 0.55,0.6,1.0, 0.6,0.5,0.0);
ncctlpts = I_Ppoint_list34(4,"3D",ncpts);
pnuni_bsp_curv(3,&ncknots,PNON_RATIONAL,&ncctlpts,0.0,0.2);
im_vclip_test1("nurb curve");

# 3. polyline
plpts = IA_Ppoint3(2, 0.4,0.5,0.0, 0.6,0.5,1.0);
pl = I_Ppoint_list3(2,plpts);
ppolyline3(&pl);
im_vclip_test1("polyline");

# 4. polymarker
pmpts = IA_Ppoint3(2, 0.4,0.5,0.0, 0.6,0.5,0.0);
pm = I_Ppoint_list3(2,pmpts);
ppolymarker3(&pm);
im_vclip_test1("polymarker");

# 5. polyline set with data
psptarr = IA_Ppoint3(2,0.4,0.5,0.0, 0.6,0.5,0.0);
lva = I_Pline_vdata_arr3(PVERT_COORD,psptarr);
lv = I_Pline_vdata_list3(2,lva);
psvdata = IA_Pline_vdata_list3(1,lv);

ppolyline_set3_data(PVERT_COORD,PMODEL_RGB,1,psvdata);
im_vclip_test1("polyline set with data");

# 6 text
txpos = I_Ppoint3( 0.4,0.5,0.0);
txdir = IA_Pvec3(2, 1.0,0.0,0.0, 0.0,1.0,0.0);
ptext3(&txpos,txdir,"regular text");
im_vclip_test1("text");

# 7. fill area
fapts = IA_Ppoint3(3, 0.4,0.4,0.0, 0.5,0.5,0.0, 0.6,0.4,0.0);
fa = I_Ppoint_list3(3,fapts);
pset_int_style(PSTYLE_HOLLOW);
pfill_area3(&fa);
im_vclip_test3("fill area");
pdel_elem();

# 8. fill area set
faspts = IA_Ppoint3(3, 0.4,0.4,0.0, 0.5,0.5,0.0, 0.6,0.4,0.0);
fas = I_Ppoint_list3(3,faspts);
fas_l = I_Ppoint_list_list3(1,&fas);
pfill_area_set3(&fas_l);
im_vclip_test2("fill area set");

# 9. fill area set with data
fwptarr = IA_Ppoint3(3, 0.4,0.4,0.0, 0.5,0.5,0.0, 0.6,0.4,0.0);
fwvarr = I_Pfacet_vdata_arr3(PVERT_COORD,fwptarr);
fwvlst = I_Pfacet_vdata_list3(3,fwvarr);
fwvdata = IA_Pfacet_vdata_list3(1,fwvlst);
fwedataarr = I_Pedge_data_arr(3,PEDGE_ON,PEDGE_ON,PEDGE_ON)
fwedata = I_Pedge_data_list(3,fwedataarr)
pfill_area_set3_data(PFACET_NONE,PEDGE_VISIBILITY,PVERT_COORD,PMODEL_RGB,NULL,1,&fwedata,fwvdata);
im_vclip_test2("fill area set with data");


# 10. quad mesh with data
qmdim = I_Pint_size(2,2);
qmptarr = IA_Ppoint3(4,0.5,0.5,0.0, 0.5,0.5,0.0, 0.4,0.4,0.0, 0.6,0.4,0.0);
qmvdata = I_Pfacet_vdata_arr3(PVERT_COORD, qmptarr);

pquad_mesh3_data(PFACET_NONE,PVERT_COORD,PMODEL_RGB,&qmdim,NULL,&qmvdata);
im_vclip_test2("quad mesh with data");

# 11. triangle strip with data
tsptarr = IA_Ppoint3(3,0.4,0.4,0.0, 0.5,0.5,0.0, 0.6,0.4,0.0);
tsvdata = I_Pfacet_vdata_arr3(PVERT_COORD, tsptarr);
 
ptri_strip3_data(PFACET_NONE,PVERT_COORD,PMODEL_RGB,3,NULL,&tsvdata); 
im_vclip_test2("triangle strip with data");
 
# 12. set of fill area set with data
ia = IA_Pint(3,0,1,2);
il = I_Pint_list(3,ia);
ill = I_Pint_list_list(1,&il);
sofavlist = IA_Pint_list_list(1,ill);

sofaedataarr = I_Pedge_data_arr(3,PEDGE_ON,PEDGE_ON,PEDGE_ON)
sofaelst = I_Pedge_data_list(3,sofaedataarr)
sofaelstlst = I_Pedge_data_list_list(1,&sofaelst)
sofaedata = IA_Pedge_data_list_list(1,sofaelstlst)

sofaptarr = IA_Ppoint3(3, 0.4,0.4,0.0, 0.5,0.5,0.0, 0.6,0.4,1.0);
sofavarr = I_Pfacet_vdata_arr3(PVERT_COORD, sofaptarr);
sofavdata = I_Pfacet_vdata_list3(3,sofavarr);

pset_of_fill_area_set3_data(PFACET_NONE,PEDGE_VISIBILITY,PVERT_COORD,PMODEL_RGB,1,NULL,sofaedata,sofavlist,&sofavdata);
im_vclip_test2("set of fill area set with data");

# 13. nurb surface
nsfloatarr = IA_Pfloat(6, 0.0,0.0,0.0,0.1,0.1,0.1);
nsknots = I_Pfloat_list(6, nsfloatarr);
nspts = IA_Ppoint3(9, 0.4,0.3,1.0, 0.5,0.35,1.0, 0.6,0.3,1.0,
                     0.4,0.4,0.5, 0.5,0.45,0.5, 0.6,0.4,0.5,
                     0.4,0.5,0.0, 0.5,0.55,0.0, 0.6,0.5,0.0);
nsgrid = I_Ppoint_grid34(3,3,"3D",nspts);

pnuni_bsp_surf(3,3,&nsknots,&nsknots,PNON_RATIONAL,&nsgrid,0,NULL)
im_vclip_test1("nurb surface");


tendtest();
